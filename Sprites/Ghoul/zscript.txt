Class Ghoul : Actor
{
  Default
  {
    Health 200;
    PainChance 24;
    Speed 9;
    Radius 24;
    Height 56;
    Mass 320;
    BloodColor "20 60 20";
    Obituary "A Ghoul poisoned %o with it's toxic projectiles.";
    HitObituary "%o was beaten to death by a ghoul.";
    SeeSound "monster/ghlsit";
    PainSound "monster/ghlpai";
    DeathSound "monster/ghldth";
    ActiveSound "monster/ghlact";
    MONSTER;
    +FLOORCLIP
    +DONTHARMCLASS
  }

  States
  {
  Spawn:
    GHUL AB 10 A_Look();
    Loop;
  See:
    GHUL AABBCCDD 2 A_Chase();
    Loop;
  Melee:
    GHUL E 0 A_Jump(128, "Melee2");
    GHUL E 6 A_FaceTarget();
    GHUL F 0 A_PlaySound("monster/ghlswg");
    GHUL F 6 A_FaceTarget();
    GHUL G 5 A_CustomMeleeAttack(4*random(1,8), "monster/ghlhit");
    Goto See;
  Melee2:
    GHUL R 6 A_FaceTarget();
    GHUL S 0 A_PlaySound("monster/ghlswg");
    GHUL S 6 A_FaceTarget();
    GHUL T 5 A_CustomMeleeAttack(4*random(1,8), "monster/ghlhit");
    Goto See;
  Missile:
    GHUL Q 10 A_FaceTarget();
    GHUL Q 8
    {
      A_SpawnProjectile("ToxinShot",44,-16,0,0,0);
      A_SpawnProjectile("ToxinShot",44, 16,0,0,0);
    }
    Goto See;
  Pain:
    GHUL H 2;
    GHUL H 2 A_Pain();
    Goto See;
  Death:
    GHUL I 5;
    GHUL J 0 A_SpawnProjectile("ToxinCloud",40,0,0,0,0);
    GHUL J 5 A_Scream();
    GHUL K 5;
    GHUL L 5 A_NoBlocking();
    GHUL M 5;
    GHUL N 5;
    GHUL O 5;
    GHUL P -1;
    Stop;
  Raise:
    GHUL PONMLKJI 5;
    Goto See;
  }
}

Class ToxinShot : Actor
{
  Default
  {
    Radius 5;
    Height 5;
    Speed 15;
    Damage 2;
    PoisonDamage 16;
    RENDERSTYLE "ADD";
    ALPHA 0.80;
    Seesound "weapons/skulfi";
    DeathSound "weapons/bloodx";
    PROJECTILE;
    +THRUGHOST
    +FLOATBOB
  }

  States
  {
  Spawn:
    GHFX A 1 Bright A_SpawnItemEx("Barbtrail", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
    GHFX A 1 Bright A_CStaffMissileSlither();
    GHFX B 1 Bright A_SpawnItemEx("Barbtrail", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
    GHFX B 1 Bright A_CStaffMissileSlither();
    loop;
  Death:
    GHFX CDEF 4 Bright;
    stop;
  }
}

Class ToxinCloud : Actor
{
  Default
  {
    Radius 0;
    Height 48;
    RENDERSTYLE "translucent";
    ReactionTime 20;
    ALPHA 0.67;
    Seesound "weapons/poof1";
    MONSTER;
    -SOLID
    -SHOOTABLE
    -ACTIVATEMCROSS
    -COUNTKILL
    +NOTELEPORT
    +THRUGHOST
    +DROPOFF
    +LOWGRAVITY
    +NODAMAGETHRUST
  }

  States
  {
  Spawn:
    GGAS ABCDEFGFD 5 A_Explode(5, 42);
    GGAS A 0 A_Countdown;
    goto Spawn+2;
  Death:
    GGAS C 5 A_FadeOut(0.10);
    GGAS C 0 A_Explode(5, 42);
    GGAS D 5 A_FadeOut(0.10);
    GGAS C 0 A_Explode(5, 42);
    GGAS E 5 A_FadeOut(0.10);
    GGAS C 0 A_Explode(5, 42);
    GGAS F 5 A_FadeOut(0.10);
    GGAS C 0 A_Explode(5, 42);
    GGAS G 5 A_FadeOut(0.10);
    GGAS C 0 A_Explode(5, 42);
    GGAS F 5 A_FadeOut(0.10);
    GGAS C 0 A_Explode(5, 42);
    GGAS E 5 A_FadeOut(0.10);
    GGAS C 0 A_Explode(5, 42);
    GGAS D 5 A_FadeOut(0.10);
    loop;
  }
}

Class Barbtrail : Actor
{
  Default
  {
    Radius 0;
    Height 1;
    PROJECTILE;
    RENDERSTYLE "ADD";
    ALPHA 0.75;
  }

  States
  {
  Spawn:
    TNT1 A 1 Bright;
    SSFX ABCDEFGH 2 Bright;
    Stop;
  }
}

